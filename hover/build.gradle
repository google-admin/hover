apply plugin: 'com.android.library'
apply plugin: 'checkstyle'

project.group = 'com.buzzvil'
project.version = '1.0.1'

android {
    compileSdkVersion project.compileSdkVersion.toInteger()

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion project.targetSdkVersion.toInteger()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api ("androidx.appcompat:appcompat:$androidXAppcompatVersion")
    api ("androidx.recyclerview:recyclerview:$androidXRecyclerviewVersion")

    testImplementation "junit:junit:$junitVersion"
}

//------ Checkstyle -------
task checkstyle(type: Checkstyle) {
    showViolations = true
    configFile file("../config/checkstyle.xml")

    source 'src/main/java'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'

    // empty classpath
    classpath = files()
}

project.afterEvaluate {
    preBuild.dependsOn('checkstyle')
    assemble.dependsOn('lint')
    check.dependsOn('checkstyle')
}

ext {
    pName = 'hover'
    pDescription = 'Buzzvil hover Android SDK'
    pPublisherVcsUrl = 'https://github.com/Buzzvil/hover.git'
    pGroup = project.group
    pVersion = project.version
}

apply from: "$rootDir/gradle/publish.gradle"